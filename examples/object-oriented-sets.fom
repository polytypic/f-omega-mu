# Object-oriented integer set implementations

# This is a direct translation of snippets from the essay
#
#   On Understanding Data Abstraction, Revisited
#   by William R. Cook.

type μISet = {
  isEmpty: bool
  contains: int → bool
  insert: int → ISet
  union: ISet → ISet
}

let μInsert: (ISet, int) → ISet = λ(s:ISet, n:int).
  if s.contains n then s else μthis:ISet.{
    isEmpty = false
    contains = λi:int.i =[int] n ∨ s.contains i
    insert = λi:int.Insert (this, i)
    union = λs:ISet.Union (this, s)
  }

and μUnion: (ISet, ISet) → ISet = λ(s1:ISet, s2:ISet).
  μthis:ISet.{
    isEmpty = s1.isEmpty ∧ s2.isEmpty
    contains = λi:int.s1.contains i ∨ s2.contains i
    insert = λi:int.Insert (this, i)
    union = λs:ISet.Union (this, s)
  }

let Empty = μthis:ISet.{
    isEmpty = true
    contains = λ_:int.false
    insert = λi:int.Insert (this, i)
    union = λs:ISet.s
  }

let Even = μthis:ISet.{
    isEmpty = false
    contains = λi:int.i % 2 =[int] 0
    insert = λi:int.Insert (this, i)
    union = λs:ISet.Union (this, s)
  }

let Full = μthis:ISet.{
   isEmpty = false
   contains = λi:int.true
   insert = λi:int.this
   union = λs:ISet.this
 }

let Interval = λ(n:int, m:int).
  μthis:ISet.{
    isEmpty = n > m
    contains = λi:int.n ≤ i ∧ i ≤ m
    insert = λi:int.Insert (this, i)
    union = λs:ISet.Union (this, s)
  }

(Empty.insert 4).union(Empty.insert 2)
