# Heterogenous map using a universal embedding

include "lib/types"

type HMap = λmap.λκ.{
  empty: map
  new_key: ∀ν.() → κ ν
  get: ∀ν.κ ν → map → opt ν
  add: ∀ν.κ ν → ν → map → map
}

let «map, «κ, HMap»»: ∃map.∃(HMap map) =
  let «univ, Univ» = import "lib/univ"
  «list univ, «λν.{to: ν → univ, of: univ → opt ν}, {
    empty = 'None
    new_key = Univ.new
    get = μget.Λν.λk.case {
        None = λ().'None
        Some = λ(u, m).
          k.of u ▷ case {
            None = λ().get[ν] k m
            Some = λv.'Some v
          }
      }
    add = Λ_.λk.λv.λm.'Some (k.to v, m)
  }»»

let k1 = HMap.new_key[int] ()
let k2 = HMap.new_key[string] ()
let m =
  HMap.empty
  ▷ HMap.add[int] k1 101
  ▷ HMap.add[string] k2 "42"

(HMap.get[int] k1 m, HMap.get[string] k2 m)
